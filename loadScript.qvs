///$tab Main
SET ThousandSep='  ';
SET DecimalSep=',';
SET MoneyThousandSep='  ';
SET MoneyDecimalSep=',';
SET MoneyFormat='$# ##0,00;-$# ##0,00';
SET TimeFormat='h:mm:ss TT';
SET DateFormat='DD.MM.YYYY';
SET TimestampFormat='DD.MM.YYYY h:mm:ss[.fff] TT';
SET FirstWeekDay=6;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='en-US';
SET CreateSearchIndexOnReload=1;
SET MonthNames='Янв;Фев;Мар;Апр;Май;Июн;Июл;Авг;Сен;Окт;Ноя;Дек';
SET LongMonthNames='Январь;Февраль;Март;Апрель;Май;Июнь;Июль;Август;Сентябрь;Октябрь;Ноябрь;Декабрь';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';
SET NumericalAbbreviation='3:тыс;6:млн;9:млрд;12:T;15:P;18:E;21:Z;24:Y;-3:тыс;-6:млн;-9:млрд;-12:p;-15:f;-18:a;-21:z;-24:y';
SET sPath = [lib://yuri.s_PersonalFolder];
///$tab Тмп
Тмп_Period:
LOAD * INLINE [
Period, PeriodList
43374,201810
43405,201811
43435,201812
];
///$tab Конструктор
КонструкторИзмеренийПростой:
LOAD * INLINE [
#КИПростой,КИПростой
1, Название МВП
2, Бизнес Юнит
3, Рынок Сбыта
4, Код Страны
];

КонструкторПоказателейПростой:
LOAD * INLINE [
#КППростой,КППростой
1, Выручка 
2, Продажи в штуках
3, Скидки 
4, Чистая прибыль 
];

КонструкторИзмеренийСводный:
LOAD * INLINE [
#КИСводный,КИСводный
1, Название МВП
2, Бизнес Юнит
3, Рынок Сбыта
4, Код Страны
5, Материал
6, Код SAP
7, Проект
];

КонструкторПоказателейСводный:
LOAD * INLINE [
#КПСводный,КПСводный
1, Выручка 
2, Продажи в штуках
3, Скидки 
4, Чистая прибыль 
];

КонструкторИзмеренийКонс_Сложный:
LOAD * INLINE [
#КИКонс,КИКонс, КИКонс_Поле
1, Название МВП, 'Исторический МВП'
2, Бизнес Юнит, 'Бизнес – Юнит'
3, Рынок Сбыта, Рынок
4, Код Страны, Страна
5, Материал, Номенклатура_Длинное_Название_Материала
6, Код SAP, %Номенклатура
7, Проект, Номенклатура_Проект
];

КонструкторПоказателейКонс_Сложный:
LOAD * INLINE [
#КПКонс,КПКонс, КПКонс_Поле, КПКонс_ПолеПлан, КПКонс_ПолеПрогноз, КПКонс_ПолеВыпонениеПлана , КПКонс_ПолеВыпонениеПрогноза
1, Выручка, '-sum({$<#Бюджетная_Форма={214}, #Факт={1}>}_Факт_Сумма*Валюта_Курс_Пересчета)','-sum({$<#Бюджетная_Форма ={214}, #Факт = {2} >} _Факт_Сумма*Валюта_Курс_Пересчета)', '-sum({$<#Бюджетная_Форма ={214}, #Факт = {3} >} _Факт_Сумма*Валюта_Курс_Пересчета)', '(-sum({$<#Бюджетная_Форма={214}, #Факт={1}>}_Факт_Сумма*Валюта_Курс_Пересчета))/(-sum({$<#Бюджетная_Форма ={214}, #Факт = {2} >} _Факт_Сумма*Валюта_Курс_Пересчета))', '(-sum({$<#Бюджетная_Форма={214}, #Факт={1}>}_Факт_Сумма*Валюта_Курс_Пересчета))/(-sum({$<#Бюджетная_Форма ={214}, #Факт = {3} >} _Факт_Сумма*Валюта_Курс_Пересчета))'
2, Продажи в штуках, 'sum({$<#Бюджетная_Форма ={ 214}, #Факт={1}>} _Факт_Количество)', 'sum({$<#Бюджетная_Форма ={214}, #Бюджетный_Период = {201801003}, #Факт = {2} >} _Факт_Количество)', 'sum({$<#Бюджетная_Форма ={214}, #Бюджетный_Период = {201803001}, #Факт = {3} >} _Факт_Количество)', '(sum({$<#Бюджетная_Форма ={ 214}, #Факт={1}>} _Факт_Количество))/(sum({$<#Бюджетная_Форма ={214}, #Бюджетный_Период = {201801003}, #Факт = {2} >} _Факт_Количество))', '(sum({$<#Бюджетная_Форма ={ 214}, #Факт={1}>} _Факт_Количество))/(sum({$<#Бюджетная_Форма ={214}, #Бюджетный_Период = {201803001}, #Факт = {3} >} _Факт_Количество))'
3, Скидки , 'sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'}, #Факт={1}  >} _Факт_Сумма*Валюта_Курс_Пересчета)', 'sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'} , #Бюджетный_Период = {201801003}, #Факт = {2} >} _Факт_Сумма*Валюта_Курс_Пересчета)', 'sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'} ,#Бюджетный_Период = {201803001}, #Факт = {3} >} _Факт_Сумма*Валюта_Курс_Пересчета)', '(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'}, #Факт={1}  >} _Факт_Сумма*Валюта_Курс_Пересчета))/(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'} , #Бюджетный_Период = {201801003}, #Факт = {2} >} _Факт_Сумма*Валюта_Курс_Пересчета))', '(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'}, #Факт={1}  >} _Факт_Сумма*Валюта_Курс_Пересчета))/(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'} ,#Бюджетный_Период = {201803001}, #Факт = {3} >} _Факт_Сумма*Валюта_Курс_Пересчета))'
4, Чистая прибыль , '(-sum({$<#Бюджетная_Форма ={214}, #Факт={1}>} _Факт_Сумма*Валюта_Курс_Пересчета))-(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'}, #Факт={1}  >} _Факт_Сумма*Валюта_Курс_Пересчета))', '(-sum({$<#Бюджетная_Форма ={214}, #Факт = {2} >} _Факт_Сумма*Валюта_Курс_Пересчета))-(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'} , #Бюджетный_Период = {201801003}, #Факт = {2} >} _Факт_Сумма*Валюта_Курс_Пересчета))', '(-sum({$<#Бюджетная_Форма ={214}, #Факт = {3} >} _Факт_Сумма*Валюта_Курс_Пересчета))-(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'} ,#Бюджетный_Период = {201803001}, #Факт = {3} >} _Факт_Сумма*Валюта_Курс_Пересчета))', '((-sum({$<#Бюджетная_Форма ={214}, #Факт={1}>} _Факт_Сумма*Валюта_Курс_Пересчета))-(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'}, #Факт={1}  >} _Факт_Сумма*Валюта_Курс_Пересчета)))/((-sum({$<#Бюджетная_Форма ={214}, #Факт = {2} >} _Факт_Сумма*Валюта_Курс_Пересчета))-(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'} , #Бюджетный_Период = {201801003}, #Факт = {2} >} _Факт_Сумма*Валюта_Курс_Пересчета)))', '((-sum({$<#Бюджетная_Форма ={214}, #Факт={1}>} _Факт_Сумма*Валюта_Курс_Пересчета))-(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'}, #Факт={1}  >} _Факт_Сумма*Валюта_Курс_Пересчета)))/((-sum({$<#Бюджетная_Форма ={214}, #Факт = {3} >} _Факт_Сумма*Валюта_Курс_Пересчета))-(sum({$<#Бюджетная_Форма ={808}, #Статья_Бюджета={'1000/464'} ,#Бюджетный_Период = {201803001}, #Факт = {3} >} _Факт_Сумма*Валюта_Курс_Пересчета)))'
];
///$tab Факт
ТаблицаФактов:
LOAD
	TEXT(NULL()) AS Тип_Продаж
AUTOGENERATE 0;

FOR EACH sPeriod IN FIELDVALUELIST('Period')

    TRACE sPeriod = $(sPeriod);
    
    LET sPeriodName = DATE('$(sPeriod)', 'MMYYYY');

    CONCATENATE(ТаблицаФактов)
    LOAD
        %Период,
        %Номенклатура,
        %ПериодВалюта,
        #Бюджетная_Форма,
        #Статья_Бюджета,
        _Количество                 AS _Факт_Количество,
        _Сумма                      AS _Факт_Сумма,
        Факт_Код_Страны,
        Факт_Рынок_Сбыта,
        Факт_Номер_Клиент           AS Дистрибьютор,
        Факт_Бизнес_Юнит,
        Факт_Валюта                 AS [Локальная валюта],
        Факт_Балансовая_Единица_TEXT,
        Факт_Код_Страны_TEXT        AS Страна,
        Факт_Рынок_Сбыта_TEXT       AS Рынок,
        Факт_Бюджетная_Форма_TEXT,
        Факт_Бизнес_Юнит_TEXT       AS [Бизнес – Юнит],
        Факт_Тип_Продаж_TEXT        AS [Тип продаж],
        Факт_Номер_Клиента_TEXT,
        Факт_Тип_Консолидации_TEXT  AS [Тип консолидации],
        Факт_Статья_Бюджета_TEXT,
        Факт_Исторический_МВП       AS [Исторический МВП],

        1                           AS #Факт
    FROM [$(sPath)/ИтоговоеЗадание/DataTier2/Факт/$(sPeriodName).qvd]
    (qvd);

    CONCATENATE(ТаблицаФактов)
    LOAD
        %Период,
        %Номенклатура,
        %ПериодВалюта,
        #Бюджетный_Период,
        #Бюджетная_Форма,
        #Статья_Бюджета,          
        _Количество                 AS _Факт_Количество,
        _Сумма                      AS _Факт_Сумма,
        План_Код_Страны,
        План_Рынок_Сбыта,
        План_Бизнес_Юнит,
        План_Валюта                 AS [Локальная валюта],
        План_Балансовая_Единица_TEXT,
        План_Код_Страны_TEXT        AS Страна,
        План_Рынок_Сбыта_TEXT       AS Рынок,
        План_Бюджетная_Форма_TEXT,
        План_Бюджетный_Период_TEXT  AS #Бюджетный_Период_TEXT,
        План_Бизнес_Юнит_TEXT       AS [Бизнес – Юнит],
        План_Тип_Продаж_TEXT        AS [Тип продаж],
        План_Тип_Консолидации_TEXT  AS [Тип консолидации],
        План_Статья_Бюджета_TEXT,
        План_Исторический_МВП       AS [Исторический МВП],

        2                           AS #Факт
    FROM [$(sPath)/ИтоговоеЗадание/DataTier2/План/$(sPeriodName).qvd]
    (qvd);

    CONCATENATE(ТаблицаФактов)
    LOAD
        %Период,
        %Номенклатура,
        %ПериодВалюта,
        #Бюджетный_Период,
        #Бюджетная_Форма,
        #Статья_Бюджета,          
        _Количество                   AS _Факт_Количество,
        _Сумма                        AS _Факт_Сумма,
        Прогноз_Код_Страны,
        Прогноз_Рынок_Сбыта,
        Прогноз_Бизнес_Юнит,
        Прогноз_Валюта                AS [Локальная валюта],
        Прогноз_Балансовая_Единица_TEXT,
        Прогноз_Код_Страны_TEXT       AS Страна,
        Прогноз_Рынок_Сбыта_TEXT      AS Рынок,          
        Прогноз_Бюджетная_Форма_TEXT,
        Прогноз_Бюджетный_Период_TEXT AS #Бюджетный_Период_TEXT,
        Прогноз_Бизнес_Юнит_TEXT      AS [Бизнес – Юнит],
        Прогноз_Тип_Продаж_TEXT       AS [Тип продаж],
        Прогноз_Тип_Консолидации_TEXT AS [Тип консолидации],
        Прогноз_Статья_Бюджета_TEXT,
        Прогноз_Исторический_МВП      AS [Исторический МВП],

        3                             AS #Факт
    FROM [$(sPath)/ИтоговоеЗадание/DataTier2/Прогноз/$(sPeriodName).qvd]
    (qvd);
    
NEXT sPeriod

DROP TABLE [Тмп_Period];
///$tab Справочники
Справочник_Номенклатуры:
LOAD
    %Номенклатура
    ,Номенклатура_Длинное_Название_Материала
    ,Номенклатура_Текущий_МВП
FROM [$(sPath)/ИтоговоеЗадание/DataTier2/Таблица_Номенклатуры.qvd]
(qvd);

Справочник_Перевода_Валюты:
LOAD
    %ПериодВалюта
    ,Валюта_Перевода
    ,Валюта_Курс_Пересчета
FROM [$(sPath)/ИтоговоеЗадание/DataTier2/Таблица_Валюта.qvd](qvd)
WHERE EXISTS(%ПериодВалюта)
;
///$tab Периоды
Temp_Calendar_Range:
LOAD
    MIN(%Период)                      AS MinDate,
    MAX(%Период)                      AS MaxDate
RESIDENT [ТаблицаФактов]
;

LET sMinDate = PEEK('MinDate', 0, 'Temp_Calendar_Range');
LET sMaxDate = PEEK('MaxDate', 0, 'Temp_Calendar_Range');

DROP TABLE Temp_Calendar_Range; 

Периоды:
LOAD 
    Temp_Date                         AS %Период,
    YEAR(Temp_Date)                   AS Год,
    MONTH(Temp_Date)                  AS Месяц,
    MONTHNAME(Temp_Date)              AS [Год - Месяц],
    'Q' & CEIL(MONTH(Temp_Date) / 3)  AS Квартал
;

LOAD 
    $(sMinDate) + ITERNO() - 1        AS Temp_Date
AUTOGENERATE(1)
WHILE 
    $(sMinDate) + ITERNO() - 1 <= $(sMaxDate);
///$tab Hidden
TAG
    КИПростой,
    КППростой,
    КИСводный,
    КПСводный,
    КИКонс, 
    КПКонс 
WITH $hidden;